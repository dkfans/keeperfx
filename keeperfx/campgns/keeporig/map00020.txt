REM LEVEL 20

REM NOTHING NEW FOR THE PLAYER (THAT'S THE LAST LEVEL! SO, IT'S NORMAL!!!)

SET_GENERATE_SPEED(600)

MAX_CREATURES(PLAYER0,25)
MAX_CREATURES(PLAYER1,30)

START_MONEY(PLAYER0,20000)
START_MONEY(PLAYER1,2460000)

COMPUTER_PLAYER(PLAYER1,0)

SET_COMPUTER_CHECKS(PLAYER1,"CHECK FOR NEUTRAL PLACES",15000,0,0,0,195000)

REM									CHECKEVERY	ATTACKPERCENT	CTADURATION		MINFORATTACK	LASTCHECK

SET_COMPUTER_CHECKS(PLAYER1,"CHECK FOR QUICK ATTACK",	700,		70,			3000,			8,			122500)
SET_COMPUTER_PROCESS(PLAYER1,"ATTACK SAFE ATTACK",-1,100,335,100,0)
SET_COMPUTER_PROCESS(PLAYER1,"ATTACK PLAN 1",-1,100,335,100,0)
SET_COMPUTER_PROCESS(PLAYER1,"ATTACK PLAN 1",-1,100,335,100,0)

REM	ALL PLAYERS

		ADD_CREATURE_TO_POOL(TROLL,30)
		ADD_CREATURE_TO_POOL(SORCEROR,17)
		ADD_CREATURE_TO_POOL(BILE_DEMON,20)
		ADD_CREATURE_TO_POOL(DARK_MISTRESS,30)
		ADD_CREATURE_TO_POOL(VAMPIRE,21)
		ADD_CREATURE_TO_POOL(DRAGON,21)
		ADD_CREATURE_TO_POOL(HELL_HOUND,30)
		ADD_CREATURE_TO_POOL(ORC,17)

REM	CREATURE AVAILABILITY

		CREATURE_AVAILABLE(ALL_PLAYERS,TROLL,1,1)
		CREATURE_AVAILABLE(PLAYER1,SORCEROR,1,1)
		CREATURE_AVAILABLE(ALL_PLAYERS,BILE_DEMON,1,1)
		CREATURE_AVAILABLE(PLAYER1,DARK_MISTRESS,1,1)
		CREATURE_AVAILABLE(PLAYER1,VAMPIRE,1,1)
		CREATURE_AVAILABLE(ALL_PLAYERS,DRAGON,1,1)
		CREATURE_AVAILABLE(ALL_PLAYERS,HELL_HOUND,1,1)
		CREATURE_AVAILABLE(PLAYER0,ORC,1,1)


REM	ROOM AVAILABILITY

		ROOM_AVAILABLE(ALL_PLAYERS,TREASURE,1,1)
		ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,1)
		ROOM_AVAILABLE(ALL_PLAYERS,GARDEN,1,1)
		ROOM_AVAILABLE(ALL_PLAYERS,TRAINING,1,1)
		ROOM_AVAILABLE(ALL_PLAYERS,RESEARCH,1,1)
		ROOM_AVAILABLE(ALL_PLAYERS,WORKSHOP,1,0)
		ROOM_AVAILABLE(ALL_PLAYERS,BARRACKS,1,0)
		ROOM_AVAILABLE(ALL_PLAYERS,GUARD_POST,1,0)
		ROOM_AVAILABLE(PLAYER1,BRIDGE,1,0)
		ROOM_AVAILABLE(ALL_PLAYERS,PRISON,1,0)
		ROOM_AVAILABLE(ALL_PLAYERS,TORTURE,1,0)
		ROOM_AVAILABLE(ALL_PLAYERS,SCAVENGER,1,0)
		ROOM_AVAILABLE(ALL_PLAYERS,TEMPLE,1,0)
		ROOM_AVAILABLE(ALL_PLAYERS,GRAVEYARD,1,0)
		
REM	SPELLS AVAILABILITY
		
		MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
		MAGIC_AVAILABLE(ALL_PLAYERS,POWER_IMP,1,1)
		MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
		MAGIC_AVAILABLE(PLAYER1,POWER_SIGHT,1,1)
		MAGIC_AVAILABLE(ALL_PLAYERS,POWER_OBEY,1,0)
		MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CALL_TO_ARMS,1,0)
		MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HEAL_CREATURE,1,0)
		MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SPEED,1,0)
		MAGIC_AVAILABLE(ALL_PLAYERS,POWER_LIGHTNING,1,0)
		MAGIC_AVAILABLE(ALL_PLAYERS,POWER_PROTECT,1,0)
		MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CONCEAL,1,0)
		MAGIC_AVAILABLE(ALL_PLAYERS,POWER_DISEASE,1,0)
		MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CAVE_IN,1,0)
		MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CHICKEN,1,0)

REM	DOORS AND TRAPS AVAILABILITY

		TRAP_AVAILABLE(ALL_PLAYERS,POISON_GAS,1,0)
		DOOR_AVAILABLE(ALL_PLAYERS,STEEL,1,0)
		TRAP_AVAILABLE(ALL_PLAYERS,BOULDER,1,0)
		DOOR_AVAILABLE(ALL_PLAYERS,MAGIC,1,0)
		TRAP_AVAILABLE(ALL_PLAYERS,LIGHTNING,1,0)
		TRAP_AVAILABLE(ALL_PLAYERS,WORD_OF_POWER,1,0)

REM  RESEARCH ORDER	

REM 134 "YOUR MISSION HERE IS TO CAPTURE THE AVATAR AND TURN THIS WHOLE WORLD EVIL. A RIVAL KEEPER CHALLENGES YOU FOR THE CROWN OF THE MOST EVIL."
REM 135 "WELL DONE,YOU HAVE DESTROYED THE AVATAR'S CASTLE, BUT YOUR RIVAL HAS ALREADY MADE OFF WITH THE CAPTURED AVATAR.ONLY THE MOST EVIL OF DUNGEON KEEPER'S CAN KILL THE AVATAR. PROVE YOUR WORTH, RAMSACK YOUR RIVALS CASTLE AND KILL THE AVATAR"
REM 136 "THE AVATAR IS DEAD BUT NOT BURIED, HE HAS RESSURECTED HIMSELF IN A HIDDEN HERO STRONGHOLD AND IS ENTHUSING HIS ARMIES TO DESTROY YOUR DUNGEON. THIS NOW IS THE ULTIMATE BATTLE BETWEEN GOOD AND EVIL, THERE CAN ONLY BE ONE SURVIVOR."

DISPLAY_OBJECTIVE(134,PLAYER0)

IF(PLAYER0,GAME_TURN >= 10)
	DISPLAY_INFORMATION(167)
ENDIF

CREATE_PARTY(RPG)
	ADD_TO_PARTY(RPG,WIZARD,10,300,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(RPG,BARBARIAN,10,300,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(RPG,KNIGHT,10,300,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(RPG,WITCH,10,300,ATTACK_DUNGEON_HEART,0)

CREATE_PARTY(BIG_BOY)
	ADD_TO_PARTY(BIG_BOY,AVATAR,10,300,ATTACK_DUNGEON_HEART,0)

CREATE_PARTY(BRUTES)
	ADD_TO_PARTY(BRUTES,GIANT,10,500,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(BRUTES,GIANT,10,500,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(BRUTES,BARBARIAN,10,500,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(BRUTES,BARBARIAN,10,500,ATTACK_DUNGEON_HEART,0)

CREATE_PARTY(MAGIC)
	ADD_TO_PARTY(MAGIC,WIZARD,10,550,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(MAGIC,FAIRY,10,550,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(MAGIC,WITCH,10,550,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(MAGIC,SAMURAI,10,550,ATTACK_DUNGEON_HEART,0)

CREATE_PARTY(DWARF)
	ADD_TO_PARTY(DWARF,DWARFA,9,650,STEAL_GOLD,0)
	ADD_TO_PARTY(DWARF,DWARFA,9,650,STEAL_GOLD,0)
	ADD_TO_PARTY(DWARF,THIEF,9,3000,STEAL_GOLD,0)
	ADD_TO_PARTY(DWARF,THIEF,9,3000,STEAL_GOLD,0)

IF(PLAYER_GOOD,DUNGEON_DESTROYED == 1)
	DISPLAY_OBJECTIVE(135,PLAYER0)
	ADD_CREATURE_TO_LEVEL(PLAYER1,VAMPIRE,PLAYER1,1,10,0)
ENDIF

IF(PLAYER_GOOD,AVATAR == 0)
	SET_FLAG(PLAYER_GOOD,FLAG1,1)
	SET_TIMER(PLAYER_GOOD,TIMER0)
ENDIF

IF(PLAYER1,DUNGEON_DESTROYED == 1)
	IF(PLAYER_GOOD,AVATAR > 0)
		DISPLAY_OBJECTIVE(169,PLAYER0)
	ENDIF
ENDIF

IF(PLAYER1,DUNGEON_DESTROYED == 1)
	ROOM_AVAILABLE(PLAYER0,BRIDGE,1,1)
ENDIF

IF(PLAYER_GOOD,FLAG1 == 1)
	IF(PLAYER_GOOD,TIMER0 >= 1000)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MAGIC,-1,1)
		ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,BRUTES,-2,DUNGEON,0,10,100)
		ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,DWARF,-3,DUNGEON,0,10,100)
		ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,DWARF,-6,DUNGEON,0,10,100)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BRUTES,1,1)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MAGIC,2,1)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BRUTES,4,1)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DWARF,3,1)
	ENDIF
	IF(PLAYER_GOOD,TIMER0 >= 1133)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DWARF,3,1)
		SET_FLAG(PLAYER_GOOD,FLAG2,1)
		PRINT("HA HA ROBBO (LOW COUNT) YOU ARE DEAD")
	ENDIF
	IF(PLAYER_GOOD,TIMER0 >= 1366)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DWARF,3,1)
		SET_FLAG(PLAYER_GOOD,FLAG2,1)
	ENDIF
	IF(PLAYER_GOOD,TIMER0 >= 1466)
		PRINT("I LIVE AGAIN. The Avatar.")
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DWARF,3,1)
		SET_FLAG(PLAYER_GOOD,FLAG2,1)
	ENDIF
	IF(PLAYER_GOOD,TIMER0 >= 1600)
		DISPLAY_OBJECTIVE(171,PLAYER0)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,RPG,3,1)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BIG_BOY,3,1)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MAGIC,2,1)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BRUTES,4,1)
		SET_FLAG(PLAYER_GOOD,FLAG2,1)
	ENDIF
	IF(PLAYER_GOOD,TIMER0 >= 1833)
		DISPLAY_OBJECTIVE(136,PLAYER0)
	ENDIF
ENDIF 

IF(PLAYER_GOOD,FLAG2 == 1)
	IF(PLAYER_GOOD,TOTAL_CREATURES == 0)
		IF(PLAYER_GOOD,DUNGEON_DESTROYED == 1)
			IF(PLAYER1,TOTAL_CREATURES == 0)
				IF(PLAYER1,DUNGEON_DESTROYED == 1)
					DISPLAY_OBJECTIVE(172,PLAYER0)
					WIN_GAME
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ENDIF

IF(PLAYER_GOOD,FLAG2 == 1)
	IF(PLAYER_GOOD,TOTAL_CREATURES == 0)
		IF(PLAYER_GOOD,DUNGEON_DESTROYED == 1)
			IF(PLAYER1,DUNGEON_DESTROYED == 0)
				DISPLAY_OBJECTIVE(173,PLAYER0)
			ENDIF
		ENDIF
	ENDIF
ENDIF

IF(PLAYER0,DUNGEON_DESTROYED == 1)
	DISPLAY_OBJECTIVE(163,PLAYER0)
	LOSE_GAME
ENDIF