REM LEVEL 10

REM NEW THINGS FOR THE PLAYER :
REM DRAGON - OK
REM HELL_HOUND - OK
REM SCAVENGER - OK
REM POWER_CAVE_IN - OK
REM TRAP_BOULDER - OK
REM POWER_CHICKEN - NOT NOW - REMOVED
REM TRAP_ALARM - NOT NOW - REMOVED
REM TRAP_LAVA - NOT NOW - REMOVED
REM DOOR_MAGIC - NOT NOW - REMOVED

SET_GENERATE_SPEED(600)

MAX_CREATURES(PLAYER0,25)
MAX_CREATURES(PLAYER1,20)

START_MONEY(PLAYER0,6000)
START_MONEY(PLAYER1,16000)

COMPUTER_PLAYER(PLAYER1,0)
SET_COMPUTER_CHECKS(PLAYER1,"CHECK FOR QUICK ATTACK",700,70,3000,8,12500)
SET_COMPUTER_PROCESS(PLAYER1,"ATTACK SAFE ATTACK",0,70,8,70,0)
SET_COMPUTER_PROCESS(PLAYER1,"ATTACK PLAN 1",0,70,8,70,0)

REM	ALL PLAYERS

REM	CREATURE AVAILABILITY

		CREATURE_AVAILABLE(ALL_PLAYERS,DARK_MISTRESS,1,1)
		CREATURE_AVAILABLE(ALL_PLAYERS,SORCEROR,1,1)
		CREATURE_AVAILABLE(ALL_PLAYERS,BILE_DEMON,1,1)
		CREATURE_AVAILABLE(ALL_PLAYERS,HELL_HOUND,1,1)
		CREATURE_AVAILABLE(ALL_PLAYERS,DRAGON,1,1)
		CREATURE_AVAILABLE(ALL_PLAYERS,ORC,1,1)
			
		ADD_CREATURE_TO_POOL(DARK_MISTRESS,15)
		ADD_CREATURE_TO_POOL(SORCEROR,20)
		ADD_CREATURE_TO_POOL(BILE_DEMON,10)
		ADD_CREATURE_TO_POOL(HELL_HOUND,20)
		ADD_CREATURE_TO_POOL(DRAGON,12)
		ADD_CREATURE_TO_POOL(ORC,12)

REM	ROOM AVAILABILITY

		ROOM_AVAILABLE(ALL_PLAYERS,TREASURE,1,1)
		ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,1)
		ROOM_AVAILABLE(ALL_PLAYERS,GARDEN,1,1)
		ROOM_AVAILABLE(ALL_PLAYERS,TRAINING,1,1)
		ROOM_AVAILABLE(ALL_PLAYERS,RESEARCH,1,1)
		ROOM_AVAILABLE(ALL_PLAYERS,WORKSHOP,1,0)
		ROOM_AVAILABLE(ALL_PLAYERS,BARRACKS,1,0)
		ROOM_AVAILABLE(ALL_PLAYERS,GUARD_POST,1,0)
		ROOM_AVAILABLE(ALL_PLAYERS,BRIDGE,1,0)
		ROOM_AVAILABLE(ALL_PLAYERS,PRISON,1,0)
		ROOM_AVAILABLE(ALL_PLAYERS,TORTURE,1,0)
		ROOM_AVAILABLE(ALL_PLAYERS,TEMPLE,1,0)
		ROOM_AVAILABLE(ALL_PLAYERS,GRAVEYARD,1,0)
		ROOM_AVAILABLE(ALL_PLAYERS,SCAVENGER,1,0)

REM	SPELLS AVAILABILITY
		
		MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
		MAGIC_AVAILABLE(ALL_PLAYERS,POWER_IMP,1,1)
		MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
		MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SIGHT,1,0)
		MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CALL_TO_ARMS,1,0)
		MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HEAL_CREATURE,1,0)
		MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SPEED,1,0)
		MAGIC_AVAILABLE(ALL_PLAYERS,POWER_LIGHTNING,1,0)
		MAGIC_AVAILABLE(ALL_PLAYERS,POWER_PROTECT,1,0)
		MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CONCEAL,1,0)
		MAGIC_AVAILABLE(ALL_PLAYERS,POWER_DISEASE,1,0)
		MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CAVE_IN,1,0)
				
REM	DOORS AND TRAPS AVAILABILITY

		DOOR_AVAILABLE(ALL_PLAYERS,WOOD,1,0)
		DOOR_AVAILABLE(ALL_PLAYERS,BRACED,1,0)
		TRAP_AVAILABLE(ALL_PLAYERS,POISON_GAS,1,0)				
		TRAP_AVAILABLE(ALL_PLAYERS,LIGHTNING,1,0)				
		DOOR_AVAILABLE(ALL_PLAYERS,STEEL,1,0)	
		TRAP_AVAILABLE(ALL_PLAYERS,BOULDER,1,0)	

REM -------------------------------------------------------------------------------------------------------------------------------------------

REM "THIS IS A FIERY REALM.  TREAD CAREFULLY.  CONQUER THE RIVAL KEEPER TO RULE THIS LAND."
DISPLAY_OBJECTIVE(100,PLAYER0)

IF(PLAYER1,DUNGEON_DESTROYED == 1)
REM	"CONGRATULATIONS.  THIS LAND AND ITS INHABITANTS WILL NOW BEND TO YOUR WILL."
	DISPLAY_OBJECTIVE(101,PLAYER0)
	WIN_GAME
ENDIF

IF_AVAILABLE(PLAYER0,BOULDER == 1)
REM	"YOU HAVE A BOULDER TRAP MANUFACTURED.  PUT THESE DOWN IN CORRIDORS AND WATCH THEM CRUSH ALL CREATURES IN ITS PATH."
	DISPLAY_INFORMATION(90)
ENDIF

IF_AVAILABLE(PLAYER0,POWER_CAVE_IN == 1)
REM	"YOU HAVE RESEARCHED THE CAVE IN SPELL.  USE IT TO BLOCK OFF PASSAGES AND REPAIR DUNGEON BREACHES.  BEWARE, HOWEVER, AS THE ROCKFALL WILL CRUSH TO DEATH ALL CREATURES BENEATH IT."
	DISPLAY_INFORMATION(91)
ENDIF

IF(PLAYER0,SCAVENGER > 0)
REM	"YOU HAVE RESEARCHED A SCAVENGER ROOM.  PLACE CREATURES WITHIN TO CONVERT OVER CREATURES OF THAT TYPE FROM ENEMY DUNGEON KEEPERS.  IF THERE ARE NO ENEMY KEEPERS, CREATURES WILL JOIN YOU FROM THE SURFACE."
	DISPLAY_INFORMATION(92)
	ROOM_AVAILABLE(PLAYER0,SCAVENGER,1,1)
ENDIF

IF(PLAYER0,DRAGON > 0)
REM	"A DRAGON HAS JOINED YOU, LURED TO YOU BY YOUR GREAT WEALTH."
	DISPLAY_INFORMATION(93)
ENDIF

IF(PLAYER0,HELL_HOUND > 0)
REM	"A HELL HOUND HAS JOINED YOU, ATTRACTED BY YOUR SCAVENGER ROOM."
	DISPLAY_INFORMATION(94)
ENDIF