REM ********************************************
REM  Ancient Keeper campaign for KeeperFX
REM  Script for Level 11
REM  Version 1.20
REM ********************************************

SET_GENERATE_SPEED(400)

COMPUTER_PLAYER(PLAYER2,1)
START_MONEY(PLAYER2,10000)

MAX_CREATURES(PLAYER0,1)
MAX_CREATURES(PLAYER2,40)

DISPLAY_OBJECTIVE(100,PLAYER0)
DISPLAY_INFORMATION(53)

ADD_CREATURE_TO_POOL(DRAGON,20)
ADD_CREATURE_TO_POOL(BILE_DEMON,20)
ADD_CREATURE_TO_POOL(ORC,20)
ADD_CREATURE_TO_POOL(DARK_MISTRESS,20)

CREATURE_AVAILABLE(ALL_PLAYERS,DRAGON,1,1)
CREATURE_AVAILABLE(PLAYER2,BILE_DEMON,1,1)
CREATURE_AVAILABLE(PLAYER2,ORC,1,1)
CREATURE_AVAILABLE(PLAYER2,DARK_MISTRESS,1,1)

ROOM_AVAILABLE(ALL_PLAYERS,TREASURE,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,GARDEN,1,1)
ROOM_AVAILABLE(PLAYER2,TRAINING,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,RESEARCH,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,BRIDGE,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,GUARD_POST,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,WORKSHOP,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,PRISON,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,TORTURE,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,BARRACKS,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,TEMPLE,1,0)

DOOR_AVAILABLE(ALL_PLAYERS,MAGIC,1,0)

MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
MAGIC_AVAILABLE(PLAYER2,POWER_IMP,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CALL_TO_ARMS,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SPEED,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_PROTECT,1,0)
MAGIC_AVAILABLE(PLAYER2,POWER_SIGHT,1,0)
MAGIC_AVAILABLE(PLAYER2,POWER_HEAL_CREATURE,1,0)

REM First, player has to claim a portal and receive a dragon from it
IF(PLAYER0,DRAGON>0)
  DISPLAY_OBJECTIVE(54,PLAYER0)
  SET_FLAG(PLAYER0,FLAG2,1)
ENDIF

REM Player is defeated when all dragons are gone before he gets his first one.
IF_AVAILABLE(PLAYER0,DRAGON <= 0)
	IF(PLAYER0,FLAG2 <= 0)
		DISPLAY_OBJECTIVE(144,PLAYER0)
		LOSE_GAME
	ENDIF
ENDIF

IF(PLAYER0,HORNY>0)
  IF(PLAYER0,FLAG1<1)
    DISPLAY_OBJECTIVE(55,PLAYER0)
    SET_FLAG(PLAYER0,FLAG1,1)
  ENDIF
ENDIF

IF(PLAYER0,SAMURAI>0)
  IF(PLAYER0,FLAG1<1)
    DISPLAY_OBJECTIVE(55,PLAYER0)
    SET_FLAG(PLAYER0,FLAG1,1)
  ENDIF
ENDIF

IF(PLAYER0,SKELETON>0)
  IF(PLAYER0,FLAG1<1)
    DISPLAY_OBJECTIVE(55,PLAYER0)
    SET_FLAG(PLAYER0,FLAG1,1)
  ENDIF
ENDIF

IF(PLAYER_GOOD,DUNGEON_DESTROYED==1)
  DISPLAY_OBJECTIVE(56,PLAYER0)
  SET_FLAG(PLAYER0,FLAG2,1)
ENDIF

IF(PLAYER0,FLAG2>0)
  IF(PLAYER2,TOTAL_CREATURES<5)
    DISPLAY_OBJECTIVE(58,PLAYER0)
  ENDIF
ENDIF

IF(PLAYER0,ALL_DUNGEONS_DESTROYED == 1)
  DISPLAY_OBJECTIVE(57,PLAYER0)
  WIN_GAME
ENDIF
