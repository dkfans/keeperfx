REM ****************************************************************************
REM  Campaigns Consolidation Project for KeeperFX strategy game.
REM ****************************************************************************
REM  Script for DD Level Batezek
REM  Campaign: Deeper Dungeons
REM  Authors:  based on Bullfrog script
REM            KeeperFX CCP Team
REM  Date:     17 Dec 1996 - 9 Sep 2014
REM  Copying and copyrights:
REM    This program is free software; you can redistribute it and/or modify
REM    it under the terms of the GNU General Public License as published by
REM    the Free Software Foundation; either version 2 of the License, or
REM    (at your option) any later version.
REM ****************************************************************************
LEVEL_VERSION(1)

SET_GENERATE_SPEED(500)

START_MONEY(PLAYER0,25000)

MAX_CREATURES(PLAYER0,20)

ADD_CREATURE_TO_POOL(HELL_HOUND,20)
ADD_CREATURE_TO_POOL(TENTACLE,20)
ADD_CREATURE_TO_POOL(SORCEROR,20)
ADD_CREATURE_TO_POOL(BILE_DEMON,20)
ADD_CREATURE_TO_POOL(DARK_MISTRESS,20)
ADD_CREATURE_TO_POOL(DRAGON,20)
ADD_CREATURE_TO_POOL(VAMPIRE,20)
ADD_CREATURE_TO_POOL(ORC,20)
ADD_CREATURE_TO_POOL(BUG,10)
ADD_CREATURE_TO_POOL(FLY,5)
ADD_CREATURE_TO_POOL(SPIDER,20)
ADD_CREATURE_TO_POOL(TROLL,20)

CREATURE_AVAILABLE(PLAYER0,HELL_HOUND,1,0)
CREATURE_AVAILABLE(PLAYER0,TENTACLE,1,0)
CREATURE_AVAILABLE(PLAYER0,SORCEROR,1,0)
CREATURE_AVAILABLE(PLAYER0,BILE_DEMON,1,0)
CREATURE_AVAILABLE(PLAYER0,DARK_MISTRESS,1,0)
CREATURE_AVAILABLE(PLAYER0,DRAGON,1,0)
CREATURE_AVAILABLE(PLAYER0,VAMPIRE,1,0)
CREATURE_AVAILABLE(PLAYER0,ORC,1,0)
CREATURE_AVAILABLE(PLAYER0,FLY,1,0)
CREATURE_AVAILABLE(PLAYER0,SPIDER,1,0)
CREATURE_AVAILABLE(PLAYER0,BUG,1,0)
CREATURE_AVAILABLE(PLAYER0,TROLL,1,0)

ROOM_AVAILABLE(PLAYER0,TREASURE,1,1)
ROOM_AVAILABLE(PLAYER0,LAIR,1,1)
ROOM_AVAILABLE(PLAYER0,GARDEN,1,1)
ROOM_AVAILABLE(PLAYER0,TRAINING,1,1)
ROOM_AVAILABLE(PLAYER0,RESEARCH,1,1)
ROOM_AVAILABLE(PLAYER0,WORKSHOP,1,0)
ROOM_AVAILABLE(PLAYER0,BARRACKS,1,0)
ROOM_AVAILABLE(PLAYER0,GUARD_POST,1,0)
ROOM_AVAILABLE(PLAYER0,TORTURE,1,0)
ROOM_AVAILABLE(PLAYER0,TEMPLE,1,0)
ROOM_AVAILABLE(PLAYER0,GRAVEYARD,1,0)

MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_IMP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
MAGIC_AVAILABLE(PLAYER1,POWER_SIGHT,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HEAL_CREATURE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SPEED,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_PROTECT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CONCEAL,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CAVE_IN,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CHICKEN,1,0)

TRAP_AVAILABLE(ALL_PLAYERS,ALARM,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,POISON_GAS,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,STEEL,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,MAGIC,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,LIGHTNING,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,WORD_OF_POWER,1,0)

CREATE_PARTY(ONE)
	ADD_TO_PARTY(ONE,THIEF,2,400,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(ONE,THIEF,2,400,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(ONE,ARCHER,2,400,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(ONE,ARCHER,2,400,ATTACK_DUNGEON_HEART,0)

CREATE_PARTY(TWO)
	ADD_TO_PARTY(TWO,ARCHER,2,600,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(TWO,ARCHER,2,600,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(TWO,DWARFA,2,600,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(TWO,DWARFA,2,600,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(TWO,DWARFA,2,600,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(TWO,DWARFA,2,600,ATTACK_DUNGEON_HEART,0)

CREATE_PARTY(THREE)
	ADD_TO_PARTY(THREE,BARBARIAN,3,500,STEAL_GOLD,0)
	ADD_TO_PARTY(THREE,BARBARIAN,3,500,STEAL_GOLD,0)
	ADD_TO_PARTY(THREE,BARBARIAN,3,500,STEAL_GOLD,0)	
	ADD_TO_PARTY(THREE,THIEF,3,500,STEAL_GOLD,0)
		
CREATE_PARTY(FOUR)
	ADD_TO_PARTY(FOUR,WITCH,3,550,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(FOUR,WITCH,3,550,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(FOUR,DWARFA,3,550,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(FOUR,DWARFA,3,550,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(FOUR,DWARFA,4,650,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(FOUR,DWARFA,4,650,ATTACK_ENEMIES,0)

CREATE_PARTY(FIVE)
	ADD_TO_PARTY(FIVE,FAIRY,4,700,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(FIVE,FAIRY,4,700,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(FIVE,FAIRY,4,700,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(FIVE,FAIRY,4,700,ATTACK_ENEMIES,0)

CREATE_PARTY(SIX)
	ADD_TO_PARTY(SIX,THIEF,4,500,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(SIX,THIEF,4,500,ATTACK_DUNGEON_HEART,0)	
	ADD_TO_PARTY(SIX,THIEF,4,500,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(SIX,THIEF,4,500,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(SIX,WIZARD,5,600,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(SIX,WIZARD,5,600,ATTACK_DUNGEON_HEART,0)

CREATE_PARTY(SEVEN)
	ADD_TO_PARTY(SEVEN,GIANT,5,1750,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(SEVEN,GIANT,5,1750,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(SEVEN,SAMURAI,5,1850,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(SEVEN,SAMURAI,5,1850,ATTACK_ENEMIES,0)

CREATE_PARTY(DEFENDA)
	ADD_TO_PARTY(DEFENDA,ARCHER,5,800,ATTACK_ENEMIES,0)															
	ADD_TO_PARTY(DEFENDA,ARCHER,5,800,ATTACK_ENEMIES,0)	
	ADD_TO_PARTY(DEFENDA,ARCHER,5,800,ATTACK_ENEMIES,0)		
	ADD_TO_PARTY(DEFENDA,ARCHER,5,800,ATTACK_ENEMIES,0)		

CREATE_PARTY(DEFENDB)
	ADD_TO_PARTY(DEFENDB,ARCHER,6,900,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(DEFENDB,ARCHER,6,900,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(DEFENDB,WIZARD,7,920,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(DEFENDB,WIZARD,7,920,ATTACK_ENEMIES,0)

CREATE_PARTY(DEFENDC)
	ADD_TO_PARTY(DEFENDC,ARCHER,7,900,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(DEFENDC,ARCHER,7,900,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(DEFENDC,ARCHER,7,900,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(DEFENDC,ARCHER,7,900,ATTACK_ENEMIES,0)

CREATE_PARTY(DEFENDD)
	ADD_TO_PARTY(DEFENDD,ARCHER,7,900,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(DEFENDD,ARCHER,7,900,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(DEFENDD,WITCH,7,1100,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(DEFENDD,WITCH,7,1100,ATTACK_ENEMIES,0)

CREATE_PARTY(LANDLORD)
	ADD_TO_PARTY(LANDLORD,BARBARIAN,9,1500,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(LANDLORD,BARBARIAN,9,1500,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(LANDLORD,BARBARIAN,9,1500,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(LANDLORD,KNIGHT,10,5000,ATTACK_DUNGEON_HEART,0)

CREATE_PARTY(DEFENDER)
	ADD_TO_PARTY(DEFENDER,GIANT,8,1200,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(DEFENDER,GIANT,8,1200,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(DEFENDER,GIANT,8,1200,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(DEFENDER,GIANT,8,1200,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(DEFENDER,GIANT,8,1200,ATTACK_ENEMIES,0)

CREATE_PARTY(MISSIONA)
	ADD_TO_PARTY(MISSIONA,THIEF,9,1500,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(MISSIONA,DWARFA,10,1500,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(MISSIONA,THIEF,9,1500,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(MISSIONA,DWARFA,10,1500,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(MISSIONA,THIEF,9,1500,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(MISSIONA,DWARFA,10,1500,ATTACK_ENEMIES,0)

CREATE_PARTY(MISSIONB)
	ADD_TO_PARTY(MISSIONB,BARBARIAN,9,1500,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(MISSIONB,GIANT,10,1200,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(MISSIONB,BARBARIAN,9,1700,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(MISSIONB,BARBARIAN,9,1700,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(MISSIONB,GIANT,10,2000,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(MISSIONB,BARBARIAN,9,1500,ATTACK_ENEMIES,0)

CREATE_PARTY(MISSIONC)
	ADD_TO_PARTY(MISSIONC,SAMURAI,9,2000,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(MISSIONC,SAMURAI,9,2000,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(MISSIONC,SAMURAI,9,2000,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(MISSIONC,SAMURAI,9,2000,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(MISSIONC,SAMURAI,9,2000,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(MISSIONC,WITCH,10,2200,ATTACK_ENEMIES,0)

REM ****************************************************************************

REM "You are surrounded on all sides by valiant heroes so show them the true meaning of evil and terror."
DISPLAY_OBJECTIVE(909,PLAYER0)

REM Room availability - by claiming, or even by reaching it

IF(PLAYER0,SCAVENGER >= 1)
	ROOM_AVAILABLE(PLAYER0,SCAVENGER,1,1)
ENDIF

IF(PLAYER0,GRAVEYARD >= 1)
	ROOM_AVAILABLE(PLAYER0,GRAVEYARD,1,1)
ENDIF

IF(PLAYER0,PRISON >= 1)
	ROOM_AVAILABLE(PLAYER0,PRISON,1,1)
ENDIF

REM The AP13 is on a claimable prison, so we only need to get to the prison to have it
IF_ACTION_POINT(13,PLAYER0)
	ROOM_AVAILABLE(PLAYER0,PRISON,1,1)
ENDIF

REM Chain of events triggered from start, sending various parties over time

IF(PLAYER0,GAME_TURN >= 30000)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,BARBARIAN,2,8,10,1000)
ENDIF

IF(PLAYER0,GAME_TURN >= 45000)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,MONK,2,8,8,1200)
ENDIF

SET_TIMER(PLAYER_GOOD,TIMER0)

IF(PLAYER_GOOD,TIMER0 >= 8000)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,ONE,-1,DUNGEON,0,1,250)
ENDIF

IF(PLAYER_GOOD,TIMER0 >= 11000)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,TWO,-2,DUNGEON,0,1,250)
ENDIF

IF(PLAYER_GOOD,TIMER0 >= 16000)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,THREE,-3,DUNGEON,0,1,250)
ENDIF

IF(PLAYER_GOOD,TIMER0 >= 19000)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,FOUR,-1,DUNGEON,0,1,250)
ENDIF

IF(PLAYER_GOOD,TIMER0 >= 22000)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,FIVE,-2,DUNGEON,0,1,250)
ENDIF

IF(PLAYER_GOOD,TIMER0 >= 27000)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,SIX,-3,DUNGEON,0,1,250)
ENDIF

IF(PLAYER_GOOD,TIMER0 >= 30000)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDA,-1,DUNGEON,0,1,250)
ENDIF

IF(PLAYER_GOOD,TIMER0 >= 35000)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDB,-3,DUNGEON,0,1,250)
ENDIF

IF(PLAYER_GOOD,TIMER0 >= 38000)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDC,-2,DUNGEON,0,1,250)
ENDIF

IF(PLAYER_GOOD,TIMER0 >= 41000)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDA,-1,DUNGEON,0,1,250)
	REM Make sure the continuous parties are disabled now - otherwise it may be too hard
	SET_FLAG(PLAYER0,FLAG3,2)
ENDIF

IF(PLAYER_GOOD,TIMER0 >= 45000)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,THREE,-2,DUNGEON,0,1,250)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,FOUR,-2,DUNGEON,0,1,250)
ENDIF

IF(PLAYER_GOOD,TIMER0 >= 49000)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,FIVE,-3,DUNGEON,0,1,250)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,FOUR,-3,DUNGEON,0,1,250)
ENDIF

IF(PLAYER_GOOD,TIMER0 >= 60000)
	IF_CONTROLS(PLAYER_GOOD,TOTAL_CREATURES <= 0)
		REM "The Lord of the Land has sent his fiercest warriors against you. Show them no mercy and flay them alive."
		DISPLAY_OBJECTIVE(910,PLAYER0)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDER,-1,1)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDER,-3,1)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDER,-4,1)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDA,-4,1)
		SET_TIMER(PLAYER0,TIMER5)
	ENDIF
ENDIF

REM Now minor parties caused by Action Points

IF_ACTION_POINT(1,PLAYER0)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDA,-2,1)
ENDIF

IF_ACTION_POINT(2,PLAYER0)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDB,-3,1)
ENDIF

IF_ACTION_POINT(3,PLAYER0)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDC,4,1)
ENDIF

IF_ACTION_POINT(5,PLAYER0)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDD,6,1)
ENDIF

IF_ACTION_POINT(7,PLAYER0)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDD,-1,1)
ENDIF

IF_ACTION_POINT(9,PLAYER0)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDB,-4,1)
ENDIF

IF_ACTION_POINT(10,PLAYER0)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDER,11,1)
ENDIF

REM Continuous party triggered by AP8 - can be only stopped by winning

IF_ACTION_POINT(8,PLAYER0)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDER,-5,1)
	SET_TIMER(PLAYER_GOOD,TIMER1)
ENDIF

IF(PLAYER_GOOD,TIMER1 >= 5500)
	NEXT_COMMAND_REUSABLE
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SIX,-5,1)
	NEXT_COMMAND_REUSABLE
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SEVEN,-4,1)
	NEXT_COMMAND_REUSABLE
	SET_TIMER(PLAYER_GOOD,TIMER1)
ENDIF

REM Code for continuously triggerable AP12

REM First trigger
IF_ACTION_POINT(12,PLAYER0)
	REM Note that we can't just set PLAYER0,FLAG3 to 1, because it could be already disabled (set to 2)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDER,-4,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THREE,-4,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FOUR,-4,1)
	SET_TIMER(PLAYER0,TIMER3)
	SET_TIMER(PLAYER0,TIMER4)
ENDIF

REM Next triggers
IF_ACTION_POINT(12,PLAYER0)
	IF(PLAYER0,FLAG3 == 0)
		REM Reset AP12 on turns 200-600 after last timer reset; we can't continue resetting it
		REM when it's spawning heroes, and we can't do reset when TIMER4 isn't started, so that's
		REM the only way to make it right. It gives 200 turns for spawning process and 400 turns
		REM for AP reset process, then the AP may be triggered again
		IF(PLAYER0,TIMER4 >= 200)
			IF(PLAYER0,TIMER4 <= 600)
				NEXT_COMMAND_REUSABLE
				RESET_ACTION_POINT(12)
			ENDIF
		ENDIF
		REM Now AP has been reset so we're sure it's triggered again, we can spawn heroes
		IF(PLAYER0,TIMER4 >= 750)
			REM Don't spawn here, only set flag - this makes sure that all parties will be spawned
			NEXT_COMMAND_REUSABLE
			SET_FLAG(PLAYER0,FLAG3,1)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER0,FLAG3 == 1)
	REM Now we can safely reset the timer - we're not in time-related conditional statement
	NEXT_COMMAND_REUSABLE
	SET_TIMER(PLAYER0,TIMER4)
	NEXT_COMMAND_REUSABLE
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THREE,-4,1)
	NEXT_COMMAND_REUSABLE
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FOUR,-4,1)
	NEXT_COMMAND_REUSABLE
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDER,-4,1)
	REM We can be sure that the command below is executed last, so all parties are created
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,FLAG3,0)
ENDIF

REM Stop after some time, but make the time long so the player will know this isn't a good way
REM These triggers are also stopped by PLAYER_GOOD,TIMER0 counter; basically, player should
REM attack this part as last; if he does, this continuous spawning will already be disabled.
IF(PLAYER0,TIMER3 >= 20000)
	SET_FLAG(PLAYER0,FLAG3,2)
ENDIF

REM Final parties triggered at the final condition on PLAYER_GOOD,TIMER0; first guards,
REM then Lord of the Land after a while

IF(PLAYER0,TIMER5 >= 500)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,MISSIONA,1,DUNGEON,0,5,500)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,MISSIONB,2,DUNGEON,0,5,500)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MISSIONC,-2,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MISSIONC,-3,1)
ENDIF

IF(PLAYER0,TIMER5 >= 750)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDER,-6,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDD,-6,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDB,-6,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,LANDLORD,-6,1)
ENDIF

IF(PLAYER0,TIMER5 >= 950)
	IF_CONTROLS(PLAYER_GOOD,KNIGHT <= 0)
		WIN_GAME
	ENDIF
ENDIF

REM ***  End of File  **********************************************************
