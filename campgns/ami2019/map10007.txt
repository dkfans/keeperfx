REM ****************************************************
REM Relic Tunnels
REM
REM Assmist Isle Campaign by Kix
REM
REM v2.0 for DAT file objectives (by dayokay)
REM 14-Nov-2020
REM ****************************************************

LEVEL_VERSION(1)

COMPUTER_PLAYER(PLAYER1,0)
COMPUTER_PLAYER(PLAYER2,0)

START_MONEY(PLAYER0,7500)
START_MONEY(PLAYER1,985000)
START_MONEY(PLAYER2,2000)

ALLY_PLAYERS(PLAYER0,PLAYER1,1)
SET_GENERATE_SPEED(80)
DEAD_CREATURES_RETURN_TO_POOL(0)

ROOM_AVAILABLE(PLAYER0,TREASURE,1,1)
ROOM_AVAILABLE(PLAYER0,LAIR,1,1)
ROOM_AVAILABLE(PLAYER0,TRAINING,1,1)
ROOM_AVAILABLE(PLAYER0,GARDEN,1,1)
ROOM_AVAILABLE(PLAYER0,WORKSHOP,1,0)

TRAP_AVAILABLE(PLAYER0,LIGHTNING,1,0)

SET_CREATURE_MAX_LEVEL(PLAYER0,HELL_HOUND,3)
SET_CREATURE_MAX_LEVEL(PLAYER0,DEMONSPAWN,3)
SET_CREATURE_MAX_LEVEL(PLAYER0,ORC,3)
SET_CREATURE_MAX_LEVEL(PLAYER0,TUNNELLER,3)
SET_CREATURE_MAX_LEVEL(PLAYER0,IMP,3)
SET_CREATURE_MAX_LEVEL(PLAYER0,SPIDER,3)
SET_CREATURE_MAX_LEVEL(PLAYER0,GHOST,3)
SET_CREATURE_MAX_LEVEL(PLAYER0,TENTACLE,3)
SET_CREATURE_MAX_LEVEL(PLAYER0,VAMPIRE,3)

MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HEAL_CREATURE,1,0)
MAGIC_AVAILABLE(PLAYER2,POWER_IMP,1,1)
MAGIC_AVAILABLE(PLAYER2,POWER_CALL_TO_ARMS,1,1)

ADD_CREATURE_TO_POOL(DARK_MISTRESS,3)
ADD_CREATURE_TO_POOL(ORC,4)
ADD_CREATURE_TO_POOL(BILE_DEMON,3)
CREATURE_AVAILABLE(PLAYER1,ORC,1,0)
CREATURE_AVAILABLE(PLAYER1,DARK_MISTRESS,1,0)
CREATURE_AVAILABLE(PLAYER1,BILE_DEMON,1,3)

CREATE_PARTY(A1)
ADD_TO_PARTY(A1,DWARFA,2,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(A1,ARCHER,1,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(A1,ARCHER,1,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(A1,MONK,1,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(A1,MONK,1,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(A1,MONK,1,0,ATTACK_DUNGEON_HEART,0)

CREATE_PARTY(A2)
ADD_TO_PARTY(A2,DWARFA,3,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(A2,MONK,2,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(A2,MONK,2,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(A2,BARBARIAN,2,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(A2,BARBARIAN,2,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(A2,WIZARD,2,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(A2,WIZARD,2,0,ATTACK_DUNGEON_HEART,0)

CREATE_PARTY(A3)
ADD_TO_PARTY(A3,DWARFA,6,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(A3,DWARFA,2,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(A3,DWARFA,2,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(A3,DWARFA,2,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(A3,DWARFA,2,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(A3,DWARFA,2,0,ATTACK_DUNGEON_HEART,0)

CREATE_PARTY(A4)
ADD_TO_PARTY(A4,DWARFA,7,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(A4,BARBARIAN,4,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(A4,BARBARIAN,4,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(A4,WIZARD,4,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(A4,WIZARD,4,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(A4,MONK,4,0,ATTACK_DUNGEON_HEART,0)

CREATE_PARTY(A5)
ADD_TO_PARTY(A5,DWARFA,7,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(A5,DWARFA,6,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(A5,ARCHER,4,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(A5,ARCHER,5,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(A5,MONK,4,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(A5,MONK,5,0,ATTACK_DUNGEON_HEART,0)

CREATE_PARTY(A6)
ADD_TO_PARTY(A6,DWARFA,7,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(A6,SAMURAI,5,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(A6,SAMURAI,5,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(A6,SAMURAI,5,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(A6,WIZARD,5,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(A6,WIZARD,4,0,ATTACK_DUNGEON_HEART,0)

CREATE_PARTY(A7)
ADD_TO_PARTY(A7,DWARFA,7,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(A7,GIANT,6,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(A7,GIANT,6,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(A7,GIANT,6,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(A7,BARBARIAN,5,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(A7,BARBARIAN,5,0,ATTACK_DUNGEON_HEART,0)


CREATE_PARTY(F1)
ADD_TO_PARTY(F1,AVATAR,10,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(F1,KNIGHT,10,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(F1,KNIGHT,10,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(F1,SAMURAI,10,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(F1,WIZARD,10,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(F1,WIZARD,10,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(F1,MONK,10,0,ATTACK_DUNGEON_HEART,0)

CREATE_PARTY(F2)
ADD_TO_PARTY(F2,ARCHER,10,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(F2,ARCHER,10,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(F2,ARCHER,10,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(F2,SAMURAI,8,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(F2,DWARFA,10,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(F2,DWARFA,10,0,ATTACK_DUNGEON_HEART,0)

CREATE_PARTY(F3)
ADD_TO_PARTY(F3,SAMURAI,10,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(F3,SAMURAI,10,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(F3,SAMURAI,8,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(F3,BARBARIAN,9,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(F3,BARBARIAN,9,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(F3,GIANT,9,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(F3,GIANT,9,0,ATTACK_DUNGEON_HEART,0)

rem action points
rem 3 first spell trigger, 4 second spell trigger
rem 1 p2 scavenger
rem 2 p0 scavenger
rem 5 p2 heart
rem 6 p2 guards

rem flags
rem 0 scavenger touched
rem 1 times spell triggered
rem 2 spell active
rem 3 progress

REVEAL_MAP_LOCATION(PLAYER0,5,25)
REVEAL_MAP_LOCATION(PLAYER0,6,25)
REVEAL_MAP_LOCATION(PLAYER0,2,33)
REVEAL_MAP_LOCATION(PLAYER0,1,15)

ADD_CREATURE_TO_LEVEL(PLAYER0,VAMPIRE,-2,IMPORT(PLAYER0,CAMPAIGN_FLAG1),1,0)

REM	QUICK_OBJECTIVE(1,"Back when the land was still settled by wizards before they were burned alive by the Polish, a keeper retreated to the northern caverns. Although discovered and defeated, he left behind a fascinating relic: a spell that is said to eliminate every living being recently exposed to sunlight. Let's find it. But first, some bad news: only allied blue keeper can attract creatures through a portal in this land. However, there are 2 scavengers capable of summoning heroes, but the powerful one has already been taken by the green keeper and the remaining one is guarded by dragons.",ALL_PLAYERS)
DISPLAY_OBJECTIVE(73,ALL_PLAYERS)

IF(PLAYER0,GAME_TURN>1000)
REM	QUICK_INFORMATION(2,"Touch the scavenger to claim it! This one summons tunnellers. Not the strongest fighters, but indeed quite useful when imps are unavailable. Some imps might still hide in the tunnels. Now, I hope you do know how to enter the area without letting the dragons in. Also do not get rid of the allied keeper before you make a use of him. Let him claim the portal somehow and convert his creatures he'll attract.",2)
	DISPLAY_INFORMATION(74,2)
ENDIF

IF(PLAYER0,GAME_TURN>2000)
REM	QUICK_INFORMATION(3,"Because except for dwarven miners noone entered the tunnels for ages, guards receded. Very tempting to use the spell right? Simply touch the relic and all heroes die! Careful, for there are only 2 altars charged with the spell so think well when to use it. Casting the spell also inactivates the scavengers!",3)
	DISPLAY_INFORMATION(75,3)
	REVEAL_MAP_LOCATION(PLAYER0,3,25)
	REVEAL_MAP_LOCATION(PLAYER0,4,25)
REM	QUICK_INFORMATION(5,"Green keeper's priestesses channel some sort of spell towards your dungeon, weakening your creatures. They cannot train above the level of 3.",5)
DISPLAY_INFORMATION(76,5)
ENDIF

IF(PLAYER0,RESEARCH>9)
	ROOM_AVAILABLE(PLAYER0,RESEARCH,1,1)
	TUTORIAL_FLASH_BUTTON(9,120)
ENDIF
IF(PLAYER0,PRISON>0)
	ROOM_AVAILABLE(PLAYER0,PRISON,1,0)
	TUTORIAL_FLASH_BUTTON(11,120)
ENDIF

IF_ACTION_POINT(2,PLAYER0)
	SET_FLAG(PLAYER0,FLAG0,1)
	rem takeover sound
	PLAY_MESSAGE(PLAYER0,SOUND,116)
	REM p0 hero spawn
	SET_TIMER(PLAYER0,TIMER0)
	SET_TIMER(PLAYER0,TIMER7)
ENDIF

IF(PLAYER2,MONEY<15000)
	NEXT_COMMAND_REUSABLE
	ADD_GOLD_TO_PLAYER(PLAYER2,15000)
ENDIF

REM p2 hero spawn
SET_TIMER(PLAYER0,TIMER1)

rem imp spawn
SET_TIMER(PLAYER0,TIMER3)
IF(PLAYER0,TIMER3>120)
	IF(PLAYER1,IMP<2)
	IF(PLAYER1,DUNGEON_DESTROYED==0)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER1,IMP,-2,1,1,0)
	ENDIF
	ENDIF
	IF(PLAYER2,IMP<2)
	IF(PLAYER2,DUNGEON_DESTROYED==0)
		IF(PLAYER2,TOTAL_CREATURES>0)
			NEXT_COMMAND_REUSABLE
			ADD_CREATURE_TO_LEVEL(PLAYER2,IMP,5,1,1,0)
		ENDIF
	ENDIF
	ENDIF
	NEXT_COMMAND_REUSABLE
	SET_TIMER(PLAYER0,TIMER3)
ENDIF


IF(PLAYER0,FLAG1==0)
	IF(PLAYER0,TIMER1>600)
		IF(PLAYER2,TOTAL_CREATURES<20)
		IF(PLAYER2,DUNGEON_DESTROYED==0)
			NEXT_COMMAND_REUSABLE
			ADD_CREATURE_TO_LEVEL(PLAYER2,WIZARD,1,1,10,0)
			NEXT_COMMAND_REUSABLE
			ADD_CREATURE_TO_LEVEL(PLAYER2,WITCH,1,1,10,0)
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER0,TIMER1)
		ENDIF
		ENDIF
	ENDIF
	IF(PLAYER0,TIMER0>600)
		IF(PLAYER0,TUNNELLER<5)
			NEXT_COMMAND_REUSABLE
			ADD_CREATURE_TO_LEVEL(PLAYER0,TUNNELLER,2,1,1,0)
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER0,TIMER0)
		ENDIF
	ENDIF
ENDIF

IF_ACTION_POINT(3,PLAYER0)
	ADD_TO_FLAG(PLAYER0,FLAG1,1)
	SET_FLAG(PLAYER0,FLAG2,1)
	REM torture
	PLAY_MESSAGE(PLAYER0,SOUND,201)
	REM apoc cast
	PLAY_MESSAGE(PLAYER0,SOUND,180)
ENDIF
IF_ACTION_POINT(4,PLAYER0)
	ADD_TO_FLAG(PLAYER0,FLAG1,1)
	SET_FLAG(PLAYER0,FLAG2,1)
	REM torture
	PLAY_MESSAGE(PLAYER0,SOUND,201)
	REM apoc cast
	PLAY_MESSAGE(PLAYER0,SOUND,180)
ENDIF

IF(PLAYER0,FLAG2>0)
	IF(PLAYER0,GOOD_CREATURES<1)
	IF(PLAYER2,GOOD_CREATURES<1)
	IF(PLAYER_GOOD,GOOD_CREATURES<1)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,FLAG2,0)
	ENDIF
	ENDIF
	ENDIF
	NEXT_COMMAND_REUSABLE
	KILL_CREATURE(ALL_PLAYERS,WIZARD,MOST_EXPERIENCED,255)
	NEXT_COMMAND_REUSABLE
	KILL_CREATURE(ALL_PLAYERS,BARBARIAN,MOST_EXPERIENCED,255)
	NEXT_COMMAND_REUSABLE
	KILL_CREATURE(ALL_PLAYERS,ARCHER,MOST_EXPERIENCED,255)
	NEXT_COMMAND_REUSABLE
	KILL_CREATURE(ALL_PLAYERS,MONK,MOST_EXPERIENCED,255)
	NEXT_COMMAND_REUSABLE
	KILL_CREATURE(ALL_PLAYERS,DWARFA,MOST_EXPERIENCED,255)
	NEXT_COMMAND_REUSABLE
	KILL_CREATURE(ALL_PLAYERS,KNIGHT,MOST_EXPERIENCED,255)
	NEXT_COMMAND_REUSABLE
	KILL_CREATURE(ALL_PLAYERS,AVATAR,MOST_EXPERIENCED,255)
	NEXT_COMMAND_REUSABLE
	KILL_CREATURE(ALL_PLAYERS,TUNNELLER,MOST_EXPERIENCED,255)
	NEXT_COMMAND_REUSABLE
	KILL_CREATURE(ALL_PLAYERS,WITCH,MOST_EXPERIENCED,255)
	NEXT_COMMAND_REUSABLE
	KILL_CREATURE(ALL_PLAYERS,GIANT,MOST_EXPERIENCED,255)
	NEXT_COMMAND_REUSABLE
	KILL_CREATURE(ALL_PLAYERS,FAIRY,MOST_EXPERIENCED,255)
	NEXT_COMMAND_REUSABLE
	KILL_CREATURE(ALL_PLAYERS,THIEF,MOST_EXPERIENCED,255)
	NEXT_COMMAND_REUSABLE
	KILL_CREATURE(ALL_PLAYERS,SAMURAI,MOST_EXPERIENCED,255)
ENDIF


IF(PLAYER0,FLAG1>0)
REM	QUICK_OBJECTIVE(4,"You touched the relic obliterating all non-dungeon native beings, making the green keeper's curse go avaunt! This has caught heroes' attention. Survive their aggression and the victory is yours! Make sure you use the remaining spell wisely.",5)
	DISPLAY_OBJECTIVE(77,5)
	SET_CREATURE_MAX_LEVEL(PLAYER0,HELL_HOUND,10)
	SET_CREATURE_MAX_LEVEL(PLAYER0,TENTACLE,10)
	SET_CREATURE_MAX_LEVEL(PLAYER0,DEMONSPAWN,10)
	SET_CREATURE_MAX_LEVEL(PLAYER0,ORC,10)
	SET_CREATURE_MAX_LEVEL(PLAYER0,TUNNELLER,10)
	SET_CREATURE_MAX_LEVEL(PLAYER0,IMP,10)
	SET_CREATURE_MAX_LEVEL(PLAYER0,SPIDER,10)
	SET_CREATURE_MAX_LEVEL(PLAYER0,GHOST,10)
	SET_CREATURE_MAX_LEVEL(PLAYER0,VAMPIRE,10)
	SET_TIMER(PLAYER0,TIMER2)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,A1,DRAWFROM(-5,-1),1)
	IF(PLAYER0,TIMER2>5000)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,A2,DRAWFROM(-5,-1),1)
	ENDIF
	IF(PLAYER0,TIMER2>11000)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,A3,DRAWFROM(-5,-1),1)
	ENDIF
	IF(PLAYER0,TIMER2>18000)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,A4,DRAWFROM(-5,-1),1)
	ENDIF
	IF(PLAYER0,TIMER2>24000)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,A5,DRAWFROM(-5,-1),1)
	ENDIF
	IF(PLAYER0,TIMER2>31000)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,A6,DRAWFROM(-5,-1),1)
	ENDIF
	IF(PLAYER0,TIMER2>38000)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,A7,DRAWFROM(-5,-1),1)
	ENDIF
	IF(PLAYER0,TIMER2>45000)
	REM	QUICK_OBJECTIVE(6,"No sign of heroes this time. Did they give up or are they stacking up? Let's hope they just took a break to pray.",ALL_PLAYERS)
		DISPLAY_OBJECTIVE(78,ALL_PLAYERS)
	ENDIF
	IF(PLAYER0,TIMER2>50000)
	REM	QUICK_OBJECTIVE(7,"Not that you wouldn't notice them in a second, but a massive mob of offense has arrived.",ALL_PLAYERS)
		DISPLAY_OBJECTIVE(79,ALL_PLAYERS)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,F1,-1,2)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,F2,-2,2)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,F2,-3,2)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,F3,-4,2)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,F3,-5,2)
	ENDIF
	IF(PLAYER0,TIMER2>55000)
		BONUS_LEVEL_TIME(5000)
	REM	QUICK_OBJECTIVE(8,"Good, heroes are becoming scarce. A concluding attack has been launched to vanquish your dungeon. The lord of the land is coming soon.",ALL_PLAYERS)
		DISPLAY_OBJECTIVE(80,ALL_PLAYERS)
	ENDIF
	IF(PLAYER0,TIMER2>60000)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,F1,-5,1)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,F2,-5,3)
	REM	QUICK_OBJECTIVE(9,"The final crew is here. Defeat them and the land is yours.",ALL_PLAYERS)
	DISPLAY_OBJECTIVE(81,PLAYER0)
		IF(PLAYER_GOOD,AVATAR==0)
		IF(PLAYER_GOOD,KNIGHT==0)
		REM	QUICK_OBJECTIVE(10,"Wonderful job, Keeper. Onto the next realm!",ALL_PLAYERS)
	DISPLAY_OBJECTIVE(82,ALL_PLAYERS)
			WIN_GAME
		ENDIF
		ENDIF
	ENDIF
ENDIF

IF(PLAYER0,TIMER7>7000)
REM	QUICK_INFORMATION(12,"The annoying humans will be entering your dungeon, exposing you more and more to the powerful keeper southwards.",7)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,A1,7,APPROPIATE_DUNGEON,0,5,500)
	DISPLAY_INFORMATION(83,7)
ENDIF

IF(PLAYER0,TIMER7>14000)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,A1,8,APPROPIATE_DUNGEON,0,5,500)
ENDIF

IF(PLAYER0,TIMER7>22500)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,A3,9,APPROPIATE_DUNGEON,0,5,500)
ENDIF

IF(PLAYER0,TIMER7>30500)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,A3,10,APPROPIATE_DUNGEON,0,5,500)
REM	QUICK_INFORMATION(11,"Humans are digging through the only opening separating you from the rival keeper!",10)
	DISPLAY_INFORMATION(84,10)
ENDIF

IF(PLAYER0,TIMER7>34000)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,A1,7,APPROPIATE_DUNGEON,0,5,500)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,A1,8,APPROPIATE_DUNGEON,0,5,500)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,A1,9,APPROPIATE_DUNGEON,0,5,500)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,A1,10,APPROPIATE_DUNGEON,0,5,500)
ENDIF

IF(PLAYER0,TIMER7>36000)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,A1,7,APPROPIATE_DUNGEON,0,5,500)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,A1,8,APPROPIATE_DUNGEON,0,5,500)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,A1,9,APPROPIATE_DUNGEON,0,5,500)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,A1,10,APPROPIATE_DUNGEON,0,5,500)
ENDIF