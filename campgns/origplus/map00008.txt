REM ****************************************************************************
REM  Campaigns Consolidation Project for KeeperFX strategy game.
REM ****************************************************************************
REM  Script for Level Tickle
REM  Campaign: New game plus
REM  Authors:  based on Bullfrog script
REM            KeeperFX CCP Team
REM            Loobinex
REM  Date:     17 Dec 1996 - 11 Jul 2020
REM  Copying and copyrights:
REM    This program is free software; you can redistribute it and/or modify
REM    it under the terms of the GNU General Public License as published by
REM    the Free Software Foundation; either version 2 of the License, or
REM    (at your option) any later version.
REM ****************************************************************************
LEVEL_VERSION(1)

REM New rooms for the player:
REM   TEMPLE
REM New creatures for the player:
REM   HORNY
REM New powers for the player:
REM   POWER_PROTECT
REM   POWER_CONCEAL
REM New manufacture for the player:
REM   STEEL

SET_GENERATE_SPEED(550)

REM Computer Player stuff
COMPUTER_PLAYER(PLAYER1,0)

START_MONEY(PLAYER0,9000)
START_MONEY(PLAYER1,12000)

MAX_CREATURES(PLAYER0,27)
MAX_CREATURES(PLAYER1,30)

ADD_CREATURE_TO_POOL(FLY,3)
ADD_CREATURE_TO_POOL(DEMONSPAWN,20)
ADD_CREATURE_TO_POOL(TROLL,20)
ADD_CREATURE_TO_POOL(DARK_MISTRESS,20)
ADD_CREATURE_TO_POOL(SORCEROR,30)
ADD_CREATURE_TO_POOL(BILE_DEMON,10)
ADD_CREATURE_TO_POOL(ORC,10)

REM	Creature availability
CREATURE_AVAILABLE(ALL_PLAYERS,FLY,1,0)
CREATURE_AVAILABLE(PLAYER0,DEMONSPAWN,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,TROLL,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,DARK_MISTRESS,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,SORCEROR,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,BILE_DEMON,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,ORC,1,0)

REM	Room availability
ROOM_AVAILABLE(ALL_PLAYERS,TREASURE,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,GARDEN,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,TRAINING,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,RESEARCH,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,WORKSHOP,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,BARRACKS,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,GUARD_POST,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,BRIDGE,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,PRISON,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,TORTURE,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,TEMPLE,1,0)

REM	Spells availability
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_IMP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SIGHT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CALL_TO_ARMS,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HEAL_CREATURE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SPEED,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_LIGHTNING,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_PROTECT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CONCEAL,1,0)

REM	Doors and traps availability
DOOR_AVAILABLE(ALL_PLAYERS,WOOD,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,BRACED,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,POISON_GAS,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,LIGHTNING,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,STEEL,1,0)

CREATE_PARTY(LANDLORD)
	ADD_TO_PARTY(LANDLORD,KNIGHT,5,3000,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(LANDLORD,MONK,4,1000,DEFEND_PARTY,0)
	ADD_TO_PARTY(LANDLORD,MONK,4,1000,DEFEND_PARTY,0)
	ADD_TO_PARTY(LANDLORD,ARCHER,4,1500,DEFEND_PARTY,0)
	ADD_TO_PARTY(LANDLORD,WIZARD,4,1500,DEFEND_PARTY,0)
	ADD_TO_PARTY(LANDLORD,WIZARD,4,1500,DEFEND_PARTY,0)

CREATE_PARTY(BACKUP)	
	ADD_TO_PARTY(BACKUP,MONK,4,1000,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(BACKUP,ARCHER,4,1500,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(BACKUP,MONK,4,1000,ATTACK_ENEMIES,0)

REM ****************************************************************************

REM "Seek out the one who would be your rival in this region of the underworld. Explain the concept of early retirement to him. On the other hand, string him up. It's in the only language he understands."
DISPLAY_OBJECTIVE(76,ALL_PLAYERS)

IF(PLAYER0,GAME_TURN <= 20)
	IF(PLAYER0,TOTAL_CREATURES >= 1)
		ADD_CREATURE_TO_LEVEL(PLAYER1,DARK_MISTRESS,PLAYER1,1,7,0)
		ADD_CREATURE_TO_LEVEL(PLAYER1,SPIDER,PLAYER1,2,4,0)
	ENDIF
ENDIF

IF(PLAYER1,PRISON >= 9)
	SET_CREATURE_TENDENCIES(PLAYER1,IMPRISON,1)
ENDIF

IF(PLAYER1,DUNGEON_DESTROYED >= 1)
	IF_CONTROLS(PLAYER_GOOD,TOTAL_CREATURES <= 0)
		SET_TIMER(PLAYER0,TIMER0)
	ENDIF
ENDIF

IF(PLAYER0,TIMER0 >= 600)
	REM "The Lord of the Land has arrived. I can smell his fear. Defeat him and the realm is yours."
	DISPLAY_OBJECTIVE(19,ALL_PLAYERS)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,LANDLORD,-1,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BACKUP,-1,1)
ENDIF

IF(PLAYER0,TIMER0 >= 800)
	IF_CONTROLS(PLAYER_GOOD,TOTAL_CREATURES <= 0)
		REM "That's another enemy successfully written off. The defenceless inhabitants of the land above bow down before your depraved presence."
		DISPLAY_OBJECTIVE(79,ALL_PLAYERS)
		WIN_GAME
	ENDIF
ENDIF

REM ****************************************************************************